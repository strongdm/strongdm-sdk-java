plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
    id 'signing'
}

def grpcVersion = '1.42.2'
def protobufVersion = '3.19.3'
def protocVersion = protobufVersion
def jsonVersion = '20190722'
def releaseVersion = System.getenv('JAVA_SDK_VERSION')

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
version = releaseVersion

archivesBaseName = "strongdm-sdk"
group = 'io.github.strongdm'

// for more info on sourceset annotations:
// https://docs.gradle.org/current/userguide/building_java_projects.html#sec:compile
sourceSets {
    main {
        java {
            srcDirs = ['com']
        }
    }
}

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
    mavenCentral()
    mavenLocal()
}

// for more info on dependency annotations:
// https://docs.gradle.org/current/userguide/building_java_projects.html#sec:java_dependency_management_overview
dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-netty:${grpcVersion}"
    implementation "org.json:json:${jsonVersion}"
    compileOnly "javax.annotation:javax.annotation-api:1.2"
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'strongdm-sdk-java'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'strongdm-sdk-java'
                description = 'strongDM SDK'
                url = 'https://github.com/strongdm/strongdm-sdk-java'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'strongdm'
                        name = 'strongDM Engineering'
                        email = 'support@strongdm.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:strongdm/strongdm-sdk-java.git'
                    developerConnection = 'scm:git:ssh://github.com:strongdm/strongdm-sdk-java.git'
                    url = 'https://github.com/strongdm/strongdm-sdk-java'
                }
            }
        }
    }
    repositories {
        maven {
            url = "https://github.com/strongdm/strongdm-sdk-java"
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
    exclude '/strongdm/api/plumbing/**'
}

nexusPublishing {
    repositories {
        sonatype {  //only for users registered in Sonatype after 24 Feb 2021
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = System.getenv("OSSRH_USERNAME")
            password = System.getenv("OSSRH_PASSWORD")
        }
    }
}
